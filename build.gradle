buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "maven"

version = getVersion()
group = modGroup
archivesBaseName = modBaseName

minecraft {
    version = project.forgeVersion
    runDir = "run"

    mappings = project.mcpVersion

    replace "@VERSION@", project.version
    replaceIn "MultiBlockStages.java"
}

repositories {
    mavenCentral()
    maven {
        name "IE/CraftTweaker"
        url "http://maven.teamacronymcoders.com"
    }
    maven {
        name "GameStages"
        url "https://maven.mcmoddev.com"
    }
}

dependencies {
    deobfCompile("blusunrize:ImmersiveEngineering:${ieVersion}") {
        exclude module: "crafttweaker"
    }
    deobfCompile("net.darkhax.gamestages:GameStages-1.12.2:${gameStageVersion}") {
        exclude module: "crafttweaker"
    }
    deobfCompile "net.darkhax.bookshelf:Bookshelf-1.12.2:${bookShelfVersion}"
    deobfCompile "CraftTweaker2:CraftTweaker2-MC1120-Main:${crafttweakerVersion}"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

String getVersion() {
    String version = modVersion
    String branch = project.hasProperty("branch") ? project.branch : ""

    if(branch != "") {
        version += "-" + branch

        String buildNumber = getBuildNumber()
        if(buildNumber != "") {
            version += "." + buildNumber
        }
    }

    return version
}

static String getBuildNumber() {
    String buildNumber = System.getenv("BUILD_NUMBER") != null ? System.getenv("BUILD_NUMBER") : ""
    if(buildNumber == "") {
        buildNumber = System.getenv("bamboo_buildNumber") != null ? "." + System.getenv("bamboo_buildNumber") : ""
    }
    return buildNumber
}
